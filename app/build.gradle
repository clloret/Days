apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'jacoco-android'
apply from: "$rootDir/app/google-services.gradle"
apply from: "$rootDir/common-android-test.gradle"
apply from: "$rootDir/config/quality.gradle"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    flavorDimensions "default"
    defaultConfig {
        applicationId "com.clloret.days"
        testApplicationId "com.clloret.days.test"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 3
        versionName "1.2.0"
        testInstrumentationRunner "com.clloret.days.utils.CustomTestRunner"

    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    signingConfigs {
        release {
            def propsFile = rootProject.file('secrets.properties')
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            resValue("string", "PORT_NUMBER", "8081")
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
    }
    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }
    adbOptions {
        installOptions '-g', '-r'
    }
    sourceSets {
        androidTest {
            resources.srcDirs += ['sampledata']
        }
    }
}
dependencies {

//------ App Dependencies ------

    implementation project(':domain')
    implementation project(':data')
    implementation project(':device')

    implementation "androidx.appcompat:appcompat:$rootProject.appCompatVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.google.firebase:firebase-analytics:17.2.3'

    implementation 'at.favre.lib:slf4j-timber:1.0.1'
    implementation "com.github.daniel-stoneuk:material-about-library:2.3.0"
    implementation "com.github.jorgecastilloprz:fabprogresscircle:1.01@aar"
    implementation "com.google.dagger:dagger:$rootProject.daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$rootProject.daggerVersion"
    implementation "com.hannesdorfmann.mosby:mvp:$rootProject.mosbyVersion"
    implementation "com.hannesdorfmann.mosby:viewstate:$rootProject.mosbyVersion"
    implementation "com.jakewharton.timber:timber:$rootProject.timberVersion"
    //noinspection AnnotationProcessorOnCompilePath
    implementation "com.jakewharton:butterknife:$rootProject.butterknifeVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"
    implementation "joda-time:joda-time:$rootProject.jodaTimeVersion"
    implementation "org.greenrobot:eventbus:$rootProject.eventBusVersion"
    implementation "com.twofortyfouram:android-plugin-api-for-locale:1.0.4"
    implementation "com.twofortyfouram:android-plugin-client-sdk-for-locale:4.0.2"

    debugImplementation "com.facebook.stetho:stetho-okhttp3:$rootProject.stethoVersion"
    debugImplementation "com.facebook.stetho:stetho:$rootProject.stethoVersion"
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'

    kapt "com.jakewharton:butterknife-compiler:$rootProject.butterknifeVersion"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"

//------ Test Dependencies ------

    testImplementation "com.google.truth:truth:$rootProject.truthVersion"
    testImplementation "junit:junit:$rootProject.junitVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"
    testImplementation "org.mockito:mockito-inline:$rootProject.mockitoVersion"

    // Core library
    androidTestImplementation "androidx.test:core:$rootProject.androidTestCore"

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation "androidx.test:runner:$rootProject.testRunnerVersion"
    androidTestImplementation "androidx.test:rules:$rootProject.testRunnerVersion"

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'

    // Espresso dependencies
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$rootProject.espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espressoVersion"

    // UI Automator
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    // Mockito
    androidTestImplementation "org.mockito:mockito-android:$rootProject.mockitoVersion"

    // Other
    androidTestImplementation "com.google.code.gson:gson:$rootProject.gsonVersion"

    // Dagger
    kaptAndroidTest "com.google.dagger:dagger-android-processor:$rootProject.daggerVersion"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
}

apply plugin: 'com.google.gms.google-services'
repositories {
    mavenCentral()
}
