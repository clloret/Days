apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'jacoco-android'
apply from: '../config/quality.gradle'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    flavorDimensions "default"
    defaultConfig {
        applicationId "com.clloret.days"
        testApplicationId "com.clloret.days.test"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "com.clloret.days.CustomTestRunner"

    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    signingConfigs {
        release {
            def propsFile = rootProject.file('secrets.properties')
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
    }
    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }
}
dependencies {

//------ App Dependencies ------

    implementation project(':domain')
    implementation project(':data')
    implementation project(':device')

    implementation "androidx.appcompat:appcompat:$rootProject.appCompatVersion"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.google.firebase:firebase-analytics:17.2.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    implementation "at.favre.lib:slf4j-timber:1.0.0"
    implementation "com.github.daniel-stoneuk:material-about-library:2.3.0"
    implementation "com.github.jorgecastilloprz:fabprogresscircle:1.01@aar"
    implementation "com.google.dagger:dagger-android-support:$rootProject.daggerVersion"
    implementation "com.google.dagger:dagger-android:$rootProject.daggerVersion"
    implementation "com.google.dagger:dagger:$rootProject.daggerVersion"
    implementation "com.hannesdorfmann.mosby:mvp:$rootProject.mosbyVersion"
    implementation "com.hannesdorfmann.mosby:viewstate:$rootProject.mosbyVersion"
    implementation "com.hannesdorfmann.parcelableplease:annotation:$rootProject.parcelablePleaseVersion"
    implementation "com.jakewharton.timber:timber:$rootProject.timberVersion"
    implementation "com.jakewharton:butterknife:$rootProject.butterknifeVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxJavaVersion"
    implementation "joda-time:joda-time:$rootProject.jodaTimeVersion"
    implementation "org.greenrobot:eventbus:$rootProject.eventBusVersion"

    debugImplementation "com.facebook.stetho:stetho-okhttp3:$rootProject.stethoVersion"
    debugImplementation "com.facebook.stetho:stetho:$rootProject.stethoVersion"

    annotationProcessor "com.google.dagger:dagger-android-processor:$rootProject.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
    annotationProcessor "com.hannesdorfmann.annotatedadapter:processor:$rootProject.annotatedAdapterVersion"
    annotationProcessor "com.hannesdorfmann.parcelableplease:processor:$rootProject.parcelablePleaseVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterknifeVersion"

//------ Test Dependencies ------

    testImplementation "com.google.truth:truth:$rootProject.truthVersion"
    testImplementation "junit:junit:$rootProject.junitVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"
    testImplementation "org.mockito:mockito-inline:$rootProject.mockitoVersion"

    // Core library
    androidTestImplementation "androidx.test:core:$rootProject.androidTestCore"

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation "androidx.test:runner:$rootProject.testRunnerVersion"
    androidTestImplementation "androidx.test:rules:$rootProject.testRunnerVersion"

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'

    // Espresso dependencies
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$rootProject.espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espressoVersion"

    // UI Automator
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    // Mockito
    androidTestImplementation "org.mockito:mockito-android:$rootProject.mockitoVersion"

    // Dagger
    androidTestAnnotationProcessor "com.google.dagger:dagger-android-processor:$rootProject.daggerVersion"
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
}

apply from: "$rootDir/app/google-services.gradle"

apply plugin: 'com.google.gms.google-services'