apply plugin: 'com.android.library'
apply plugin: 'jacoco-android'
apply from: "$rootDir/common-android-test.gradle"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    testOptions.unitTests.includeAndroidResources = true

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {
    implementation(project(':domain')) {
        exclude group: "com.intellij", module: "annotations"
    }

    api "androidx.room:room-runtime:$rootProject.roomVersion"
    implementation "androidx.room:room-rxjava2:$rootProject.roomVersion"
    implementation "com.fasterxml.jackson.core:jackson-core:$rootProject.jacksonVersion"
    implementation "com.fasterxml.jackson.core:jackson-databind:$rootProject.jacksonVersion"
    implementation "com.jakewharton.timber:timber:$rootProject.timberVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxJavaVersion"
    implementation "joda-time:joda-time:$rootProject.jodaTimeVersion"
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'com.github.clloret:airtable.android:2f84539e51'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'javax.inject:javax.inject:1'

    annotationProcessor "androidx.room:room-compiler:$rootProject.roomVersion"

    //------ Test Dependencies ------

    testImplementation "androidx.test:core:$rootProject.androidTestCore"
    testImplementation "com.google.truth:truth:$rootProject.truthVersion"
    testImplementation "com.squareup.okhttp3:mockwebserver:$rootProject.mockWebServerVersion"
    testImplementation "junit:junit:$rootProject.junitVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"
    testImplementation 'android.arch.lifecycle:livedata:1.1.1'
    testImplementation 'org.robolectric:robolectric:4.3.1'

    // Core library
    androidTestImplementation "androidx.test:core:$rootProject.androidTestCore"

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation "androidx.test:runner:$rootProject.testRunnerVersion"
    androidTestImplementation "androidx.test:rules:$rootProject.testRunnerVersion"

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation "com.google.truth:truth:$rootProject.truthVersion"

    // Espresso dependencies
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espressoVersion"

    // Room testing
    androidTestImplementation "androidx.room:room-testing:$rootProject.roomVersion"

    // InstantTaskExecutorRule
    // Version 2.0.1 is a requirement of androidx.room:room-testing:2.2.5
    //noinspection GradleDependency
    androidTestImplementation 'androidx.arch.core:core-testing:2.0.1'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}
